/* This is a prototype of a domain-specific language for CARP simulations.
 *
 * Author: Philip Gemmell
 * email: philip.gemmell@protonmail.com
 */

CarpSimulation:
    "carp_simulation:"
        commands*=Command
;

Command:
    MeshPrepare | StimulusCommand | RunCommand | DryRun
;

DryRun:
    "dry run"
;

MeshPrepare:
    SetMesh | CreateMesh | UseMesh
;

SetMesh:
    "set mesh" setting=MeshSetCmd ((x=FLOAT "x" y=FLOAT "x" z=FLOAT) | (units=MeshSize))
;

MeshSize:
    "mm" | "um"
;

MeshSetCmd:
    "size" | "resolution" | "units to be"
;

CreateMesh:
    "create mesh" ("called" name=STRING)? ("in folder" folder=STRING)?
;

UseMesh:
    "use mesh at" folder=STRING
;

StimulusCommand:
    "stimulate mesh for" duration=FLOAT "ms with" strength=FLOAT "mA"
    "for region of size (" size_x=FLOAT "," size_y=FLOAT "," size_z=FLOAT ")"
    "at (" loc_x=FLOAT "," loc_y=FLOAT "," loc_z=FLOAT ")"
;

RunCommand:
    "run" (sim_type=SimType)? "simulation for " duration=FLOAT "ms" ("with output to" output=STRING)?
;

SimType:
    "monodomain" | "bidomain"
;

// Special rule for comments. Comments start with //
Comment:
    /\/\/.*$/
;