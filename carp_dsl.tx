/* This is a prototype of a domain-specific language for CARP simulations.
 *
 * Author: Philip Gemmell
 * Email: philip.gemmell@protonmail.com
 */

CarpSimulation:
    "carp_simulation:"
        commands*=Command
;

Command:
    MeshCommand | PrepareCommand | RunCommand | DryRun
;

DryRun:
    cmd="dry run"
;

MeshCommand:
    SetMesh | CreateMesh | UseMesh
;

PrepareCommand:
    StimulusCommand | ParamFileCommand | RegionCommand
;

SetMesh:
    "set mesh" setting=MeshSetCmd
    (("(" x=FLOAT "x" y=FLOAT "x" z=FLOAT ")") | ("(" x=FLOAT "," y=FLOAT "," z=FLOAT ")")) meshUnits=MeshSize
;

MeshSize:
    "cm" | "mm" | "um"
;

MeshSetCmd:
    "size" | "resolution" | "centre at"
;

CreateMesh:
    "create mesh" ("called" name=STRING)? ("in folder" folder=STRING)?
;

UseMesh:
    "use mesh at" folder=STRING
;

StimulusCommand:
    "stimulate mesh"
        ("at" start=FLOAT "ms")?
        ("every" bcl=FLOAT "ms" ("for" n_pulse=INT "times")?)?
        "for" duration=FLOAT "ms"
        "with" strength=FLOAT "pA/pF"
        "for region" (loc_cmd=LocationCommand | region=STRING)
;

ParamFileCommand:
    "use input file" param_file=STRING
;

RegionCommand:
    "define region" name=STRING (TagCommand | LocationCommand)
;

TagCommand:
    "using tag" tag=INT
;

LocationCommand:
    "at ("loc_x=FLOAT "," loc_y=FLOAT "," loc_z=FLOAT ")" loc_units=MeshSize
    "of size (" size_x=FLOAT "," size_y=FLOAT "," size_z=FLOAT ")" size_units=MeshSize
;

RunCommand:
    "run" (sim_type=SimType)? "simulation for " duration=FLOAT "ms" ("with output to" output=STRING)?
;

SimType:
    "monodomain" | "bidomain"
;

// Special rule for comments. Comments start with //
Comment:
    /\/\/.*$/
;